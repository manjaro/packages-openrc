# $Id$
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>

_pkgname=rpcbind

pkgname=rpcbind-nosystemd
pkgver=0.2.4
pkgrel=1
pkgdesc="portmap replacement which supports RPC over various protocols"
arch=(i686 x86_64)
backup=('etc/conf.d/rpcbind')
makedepends=('eudev')
depends=('bash' 'glibc' 'libtirpc' 'libeudev')
provides=("$_pkgname")
conflicts=("$_pkgname")
url="http://rpcbind.sourceforge.net"
license=('custom')
replaces=('portmap')
# see also http://git.infradead.org/users/steved/rpcbind.git
source=(https://downloads.sourceforge.net/sourceforge/$_pkgname/$_pkgname-$pkgver.tar.bz2
        rpcbind-sunrpc.patch
        rpcbind.conf)
install=rpcbind.install
sha1sums=('8a6045dd3397e9f71bf3a7c9d269e255cca537bd'
          '1b997ce76f9727efc8c72fc5f97189591c9a60e2'
          '08246ff18d12cee01e5c9391c6ba8f5597cac936')

prepare() {
  cd $srcdir/$_pkgname-$pkgver
  # patch for iana services file # FS#20273
  patch -Np1 -i ../rpcbind-sunrpc.patch
}

build() {
  cd $srcdir/$_pkgname-$pkgver
  ./configure --prefix=/usr \
    --with-rpcuser=rpc \
    --enable-warmstarts \
    --with-systemdsystemunitdir=no \
    --with-statedir=/var/lib/rpcbind
  make
}

package() {
  cd $srcdir/$_pkgname-$pkgver
  make DESTDIR=$pkgdir install
  # install rpcbind config file
  install -D -m644 $srcdir/rpcbind.conf $pkgdir/etc/conf.d/rpcbind
  # install missing man page - https://bugs.archlinux.org/task/21271
  install -m644 man/rpcinfo.8 $pkgdir/usr/share/man/man8/
  # add state directory
  install -d -m 700 -o 32 -g 32 $pkgdir/var/lib/rpcbind
  # install license
  install -D -m644 COPYING $pkgdir/usr/share/licenses/rpcbind/COPYING
}
