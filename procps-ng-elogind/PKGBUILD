# Maintainer: Alexey D. <lq07829icatm@rambler.ru>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

pkgname=procps-ng-elogind
pkgver=3.3.12
pkgrel=2
pkgdesc='Utilities for monitoring your system and its processes'
url='https://gitlab.com/procps-ng/procps'
license=('GPL' 'LGPL')
arch=('i686' 'x86_64')
# groups=('base')
depends=('ncurses' 'libelogind')
provides=('procps' "procps-ng=$pkgver" 'sysvinit-tools')
conflicts=('procps' 'procps-ng' 'sysvinit-tools' 'sysctl-default-conf')
replaces=('procps' 'sysvinit-tools' 'sysctl-default-conf')
backup=('etc/sysctl.conf')
install=procps-ng.install
source=("http://downloads.sourceforge.net/project/procps-ng/Production/procps-ng-${pkgver}.tar.xz"
        'sysctl.conf'
        'procps-enable-elogind.patch')
sha1sums=('82c0745f150f1385ca01fe7d24f05f74e31c94c6'
          '674282245d8ab2e09017b8f8cdce20a3ff81e631'
          '8bc215d118b3c7f2006950c34664f597f1c253e5')

prepare() {
	cd "${srcdir}/procps-ng-${pkgver}"

	sed 's:<ncursesw/:<:g' -i watch.c
	patch -Np 1 -i ${srcdir}/procps-enable-elogind.patch
	autoreconf -fi
}

build() {
	cd "${srcdir}/procps-ng-${pkgver}"

	./configure \
		--prefix=/usr \
		--exec-prefix=/ \
		--sysconfdir=/etc \
		--libdir=/usr/lib \
		--bindir=/usr/bin \
		--sbindir=/usr/bin \
		--with-elogind \
		--enable-watch8bit
	make
}

package() {
	cd "${srcdir}/procps-ng-${pkgver}"

	make DESTDIR="${pkgdir}" install
	install -Dm644 "sysctl.conf" "$pkgdir/etc/sysctl.conf"

	# provided by util-linux
	rm "${pkgdir}/usr/bin/kill"
	rm "${pkgdir}/usr/share/man/man1/kill.1"
}
